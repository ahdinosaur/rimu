@top Template { block }

@precedence {
  get
  call
  unary
  factor @left
  term @left
  comparison @left
  equality @left
  xor @left
  and @left
  or @left
  objectBlockEntry // why?
}

block[@isGroup=Block] {
  ListBlock |
  ObjectBlock |
  expressionBlock { expression lineEnd } |
  IndentedBlock { endOfLine indent block dedentEnd }
//  BlockFunction |
//  BlockCall |
//  BlockLet |
//  BlockIf
}

ListBlock { listBlockItem+ }
listBlockItem { listItemMarker block dedentEnd }

ObjectKey { Identifier | String }
ObjectBlockEntry { !objectBlockEntry ObjectKey ':' block }
ObjectBlock { ObjectBlockEntry+ }

expression[@isGroup=Expression] {
  Null |
  Boolean |
  String |
  Number |
  Identifier ~arrow |
  ParenthesizedExpression |
  ListExpression |
  ObjectExpression |
  FunctionExpression |
  CallExpression |
  GetExpression |
  UnaryExpression |
  BinaryExpression
}

ParenthesizedExpression { '(' expression ')' }

ListExpression { '[' commaSep<expression> ']' }

ObjectExpressionEntry { ObjectKey ':' expression }
ObjectExpression { '{' commaSep<ObjectExpressionEntry> '}' }

FunctionExpressionParams { '(' commaSep<Identifier ~arrow> ')' }
FunctionExpression { FunctionExpressionParams '=>' expression }

CallExpression {
  expression !call CallExpressionArgs
}

CallExpressionArgs { '(' commaSep<expression> ')' }

GetExpression {
  expression !get (
    '.' Identifier |
    '[' expression ']' |
    '[' expression? ':' expression? ']'
  )
}

UnaryExpression {
  (ArithmeticOp<'-'> | LogicOp<'!'>)
  expression
}

BinaryExpression {
  expression !factor ArithmeticOp<'*' | '/' | '%'> expression |
  expression !term ArithmeticOp<'+' | '-'> expression |
  expression !comparison CompareOp<'>' | '>=' | '<' | '<='> expression |
  expression !equality CompareOp<'==' | '!='> expression |
  expression !xor CompareOp<'^'> expression |
  expression !and CompareOp<'&&'> expression |
  expression !or CompareOp<'||'> expression
}

commaSep<content> {
  "" | content ("," content?)*
}

lineEnd { endOfLine | endOfFile }
dedentEnd { dedent | endOfFile }

@skip {
  spaces |
  LineComment |
  blankLineStart (spaces | LineComment)* lineEnd
}

@context trackIndent from './tokens.js'

@external tokens newlines from './tokens.js' {
  endOfLine
  endOfFile
  blankLineStart
}

@external tokens indentation from './tokens.js' {
  indent
  dedent
}

@external tokens listItemMarkers from './tokens.js' {
  listItemMarker
}

@tokens {
  spaces { $[ ]+ }

  @precedence {
    Null,
    Boolean,
    Identifier
  }

  Null { 'null' }
  Boolean { 'false' | 'true' }

  String { '"' (!["\\] | '\\' _)* '"' }

  Number { int frac? }
  int { @digit+ }
  frac { '.' @digit+ }

  Identifier { $[a-zA-Z_] $[a-zA-Z0-9_]* }

  '(' ')'
  '[' ']'
  '{' '}'

  'if' 'then' 'else'
  'let' 'in'
  '=>'[@name=FatArrow]

  ArithmeticOp<expr> { expr }
  LogicOp<expr> { expr }
  CompareOp<expr> { expr }

  LineComment { '#' ![\n\r]* }
}

@detectDelim
